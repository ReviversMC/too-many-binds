plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower' version '1.7.3'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16


archivesBaseName = project.archives_base_name
def mod_version = project.mod_version as Object
version = "${mod_version}+1.17"

apply from: '../common.gradle'


dependencies {
    include api(project(path: ":toomanybinds-1.16", configuration: "namedElements"))

    minecraft "com.mojang:minecraft:${project.minecraft_version_1_17}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings_1_17}:v2"

    // Required Fabric API modules
    modImplementation(fabricApi.module("fabric-api-base", project.fabric_version_1_17))
    modImplementation(fabricApi.module("fabric-lifecycle-events-v1", project.fabric_version_1_17))
    modImplementation(fabricApi.module("fabric-key-binding-api-v1", project.fabric_version_1_17))

    // Use the full Fabric API while debugging, otherwise ModMenu complains
    modRuntimeOnly "net.fabricmc.fabric-api:fabric-api:${project.fabric_version_1_17}"

    // Amecs API
    include modImplementation("de.siphalor:amecsapi-1.17:${project.amecs_api_version_1_17}")

    // ModMenu integration
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version_1_17}"
    // Cloth Config for config screen
    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version_1_17}") {
        exclude(group: "net.fabricmc.fabric-api")
    }


    if (project.use_third_party_mods == "true") {
        configurations {
            modRuntimeOnly {
                transitive = true
                exclude module: "fabric-loader"
                exclude module: "fabric-api-base"
                exclude module: "fabric-lifecycle-events-v1"
                exclude module: "fabric-key-binding-api-v1"
            }
        }

        modRuntimeOnly "maven.modrinth:lazydfu:${project.lazydfu_version_1_17}"
        modRuntimeOnly "maven.modrinth:sodium:${project.sodium_version_1_17}"
        runtimeOnly "org.joml:joml:${project.joml_version_sodium_1_17}"
        modRuntimeOnly "maven.modrinth:lithium:${project.lithium_version_1_17}"
        modRuntimeOnly "maven.modrinth:starlight:${project.starlight_version_1_17}"
        modRuntimeOnly "maven.modrinth:smoothboot-fabric:${project.smoothboot_version_1_17}"
        modRuntimeOnly "curse.maven:no-fade-452768:${project.no_fade_version_1_17}"
    }
}
