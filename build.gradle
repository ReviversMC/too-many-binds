plugins {
	id 'fabric-loom' apply false
	id 'io.github.juuxel.loom-quiltflower' apply false
	id 'checkstyle'
}


allprojects {
	apply plugin: 'fabric-loom'
	apply plugin: 'io.github.juuxel.loom-quiltflower'
	apply plugin: 'checkstyle'

	checkstyle {
		configFile = file("${rootDir}/checkstyle.xml")
		toolVersion = project.checkstyle_version
	}


	version = rootProject.mod_version
	group = rootProject.maven_group
	archivesBaseName = rootProject.archives_base_name


	configurations {
		modRuntimeOnly {
			transitive = true
			exclude module: 'fabric-loader'
			exclude module: 'fabric-api-base'
		}
	}

	repositories {
		if (project.use_third_party_mods == 'true') {
			maven {
				name = 'Modrinth'
				url = 'https://api.modrinth.com/maven'
				content {
					includeGroup 'maven.modrinth'
				}
			}
			maven {
				url = 'https://cursemaven.com'
				content {
					includeGroup 'curse.maven'
				}
			}
		}
		maven {
			name "Siphalor's Maven"
			url 'https://maven.siphalor.de/'
		}
		maven {
			name = 'TerraformersMC'
			url = 'https://maven.terraformersmc.com/releases'
		}
		maven {
			url = 'https://maven.shedaniel.me/'
		}
		maven {
			name = 'JitPack'
			url = 'https://jitpack.io'
		}
		maven {
			name = 'NucleoidMC'
			url = 'https://maven.nucleoid.xyz/'
		}
		mavenCentral()
	}

	dependencies {
		modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"

		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

		// Required Fabric API modules
		modImplementation fabricApi.module('fabric-api-base', project.fabric_api_version)
		modImplementation fabricApi.module('fabric-lifecycle-events-v1', project.fabric_api_version)
		modImplementation fabricApi.module('fabric-key-binding-api-v1', project.fabric_api_version)

		// Amecs API
		//modImplementation "de.siphalor:amecsapi-1.19:${project.amecs_api_version}"

		// ModMenu
		modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
		// Cloth Config
		modImplementation("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
			exclude(group: 'net.fabricmc.fabric-api')
		}

		if (project.use_third_party_mods == 'true') {
			configurations {
				modRuntimeOnly {
					exclude module: 'fabric-lifecycle-events-v1'
					exclude module: 'fabric-key-binding-api-v1'
				}
			}

			modRuntimeOnly "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
			//modRuntimeOnly "maven.modrinth:sodium:${project.sodium_version}"
			//modRuntimeOnly "maven.modrinth:lithium:${project.lithium_version}"
			modRuntimeOnly "maven.modrinth:starlight:${project.starlight_version}"
			//modRuntimeOnly "maven.modrinth:smoothboot-fabric:${project.smoothboot_version}"
			modRuntimeOnly "curse.maven:no-fade-452768:${project.no_fade_version}"
		}
	}


	processResources {
		inputs.property 'version', project.version

		filesMatching('fabric.mod.json') {
			expand 'version': project.version
		}
	}

	tasks.withType(JavaCompile).configureEach {
		// Ensure that the encoding is set to UTF-8, no matter what the system default is.
		// This fixes some edge cases with special characters not displaying correctly,
		// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html.
		// If Javadoc is generated, this must be specified in that task too.
		it.options.encoding = 'UTF-8'
	}

	java {
		// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
		// if it is present. If you remove this line, sources will not be generated.
		withSourcesJar()

		// If this mod is going to be a library, then it should also generate Javadocs in order to aid
		// with development. Uncomment this line to generate them.
		// withJavadocJar()
	}
}


processResources {
	from("${rootDir}/assets/icon.png") {
		into "assets/toomanybinds/"
	}
}
