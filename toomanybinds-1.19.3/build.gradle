plugins {
	id 'fabric-loom'
	id 'io.github.juuxel.loom-quiltflower'
	id 'checkstyle'
	id 'com.github.spotbugs'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17


archivesBaseName = project.archives_base_name
def mod_version = project.mod_version as Object
version = "${mod_version}+1.19.3"

apply from: '../common.gradle'


dependencies {
	include implementation(project(path: ':toomanybinds-1.19.3-compat', configuration: 'namedElements'))

	minecraft "com.mojang:minecraft:${project.minecraft_version_1_19_3}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings_1_19_3}:v2"

	// Required Fabric API modules
	modImplementation fabricApi.module('fabric-api-base', project.fabric_api_version_1_19_3)
	modImplementation fabricApi.module('fabric-lifecycle-events-v1', project.fabric_api_version_1_19_3)
	modImplementation fabricApi.module('fabric-key-binding-api-v1', project.fabric_api_version_1_19_3)

	// Amecs API
	modImplementation "de.siphalor:amecsapi-1.19:${project.amecs_api_version_1_19_3}"
	// Nmuk version that is compatible with latest Fabric API
	modImplementation "de.siphalor:nmuk-1.19:${project.nmuk_version_1_19_3}"

	// ModMenu
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version_1_19_3}"
	// Cloth Config
	modImplementation("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version_1_19_3}") {
		exclude(group: 'net.fabricmc.fabric-api')
	}


	if (project.use_third_party_mods == 'true') {
		configurations {
			modRuntimeOnly {
				exclude module: 'fabric-lifecycle-events-v1'
				exclude module: 'fabric-key-binding-api-v1'
			}
		}

		modRuntimeOnly "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version_1_19_3}"
		modRuntimeOnly "maven.modrinth:lazydfu:${project.lazydfu_version_1_19_3}"
		modRuntimeOnly "maven.modrinth:sodium:${project.sodium_version_1_19_3}"
		modRuntimeOnly "maven.modrinth:lithium:${project.lithium_version_1_19_3}"
		modRuntimeOnly "maven.modrinth:starlight:${project.starlight_version_1_19_3}"
		modRuntimeOnly "maven.modrinth:smoothboot-fabric:${project.smoothboot_version_1_19_3}"
		modRuntimeOnly "curse.maven:no-fade-452768:${project.no_fade_version_1_19_3}"
	}
}


tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18-pre2 upwards uses Java 17.
	it.options.release = 17
}
